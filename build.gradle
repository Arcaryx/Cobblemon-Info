plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id 'maven-publish'
	id 'org.jetbrains.kotlin.jvm' version "1.7.10"
}

version = "fabric-" + project.minecraft_version + "-" + project.mod_version
group = mod_base_package

base {
	archivesName = project.mod_id
}

repositories {
	maven { url = 'https://maven.parchmentmc.org' }
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup 'curse.maven'
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:${mappings_channel}-${mappings_version}@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "curse.maven:cobblemon-687131:${cobblemon_file}"
	modImplementation "curse.maven:architectury-419699:${architectury_file}"
	modImplementation "curse.maven:jei-238222:${jei_file}"
	modImplementation "curse.maven:jer-240630:${jer_file}"
	modImplementation "curse.maven:jade-324717:${jade_file}"
}

def replaceProperties = [mod_id          : mod_id,
						 mod_name        : mod_name,
						 mod_version     : mod_version,
						 mod_authors     : mod_authors,
						 mod_description : mod_description,
						 mod_base_package: mod_base_package,
						 minecraft_version: minecraft_version,
						 fabric_version  : fabric_version,
						 loader_version  : loader_version]

processResources {
	inputs.properties replaceProperties

	filesMatching("fabric.mod.json") {
		expand replaceProperties
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.mod_id}"}
	}
}